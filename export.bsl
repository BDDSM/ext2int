
Процедура КнопкаНажатие(Элемент)
	
	Т = Новый ТекстовыйДокумент;
	
	Выгрузить("Отчет", Т);
	Выгрузить("Обработка", Т);
	Выгрузить("ЗаполнениеТабличныхЧастей", Т);
	Выгрузить("ПечатнаяФорма", Т);
	
	Т.Показать();
	
КонецПроцедуры

Процедура Выгрузить(ВидОбработки, Т)
	
	НовыеИмена = Новый Соответствие; // {НовоеИмя, (УИД, НомерСтроки)}
	СтарыеИмена = Новый Соответствие; // {УИД_НомерСтроки, НовоеИмя}
	
	#Область Запросы
	
	ТекстЗапросаОтчетОбработкаЗТЧ = 
	"ВЫБРАТЬ
	|	ВнешниеОбработки.Ссылка,
	|	ВнешниеОбработки.Наименование,
	|	ВнешниеОбработки.ХранилищеВнешнейОбработки,
	|	ВнешниеОбработки.Код,
	|	0 КАК НомерСтроки
	|ИЗ
	|	Справочник.ВнешниеОбработки КАК ВнешниеОбработки
	|ГДЕ
	|	ВнешниеОбработки.ВидОбработки = &ВидОбработки";
	
	ТекстЗапросаПечатнаяФорма = 
	"ВЫБРАТЬ
	|	ВнешниеОбработкиПринадлежность.Ссылка,
	|	ВнешниеОбработкиПринадлежность.Ссылка.ХранилищеВнешнейОбработки,
	|	ВнешниеОбработкиПринадлежность.Ссылка.Наименование,
	|	ВнешниеОбработкиПринадлежность.Ссылка.Код,
	|	ВнешниеОбработкиПринадлежность.НомерСтроки,
	|	ВнешниеОбработкиПринадлежность.ХранилищеВнешнейОбработки КАК ХранилищеВнешнейОбработкиДополнительное
	|ИЗ
	|	Справочник.ВнешниеОбработки.Принадлежность КАК ВнешниеОбработкиПринадлежность
	|ГДЕ
	|	ВнешниеОбработкиПринадлежность.Ссылка.ВидОбработки = &ВидОбработки";
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидОбработки", Перечисления.ВидыДополнительныхВнешнихОбработок[ВидОбработки]);
	
	Если ВидОбработки = "Отчет" Тогда
		Префикс = "ВО_ОТЧ_";
		Менеджер = ВнешниеОтчеты;
		ПрефиксНастроек = "ВнешнийОтчетОбъект.";
		ПрефиксНовыхНастроек = "ОтчетОбъект.";
		Запрос.Текст = ТекстЗапросаОтчетОбработкаЗТЧ;
		Расширение = "erf";
		Тэг = "Report";
	Иначе
		ПрефиксНастроек = "";
		Менеджер = ВнешниеОбработки;
		Расширение = "epf";
		Тэг = "DataProcessor";
		Если ВидОбработки = "Обработка" Тогда
			Префикс = "ВО_ОБР_";
			ПрефиксНастроек = "ВнешняяОбработкаОбъект.";
			ПрефиксНовыхНастроек = "ОбработкаОбъект.";
			Запрос.Текст = ТекстЗапросаОтчетОбработкаЗТЧ;
		ИначеЕсли ВидОбработки = "ЗаполнениеТабличныхЧастей" Тогда
			Префикс = "ВО_ЗТЧ_";
			Запрос.Текст = ТекстЗапросаОтчетОбработкаЗТЧ;
		ИначеЕсли ВидОбработки = "ПечатнаяФорма" Тогда
			Префикс = "ВО_ПФ_";
			Запрос.Текст = ТекстЗапросаПечатнаяФорма;
		Иначе
			ВызватьИсключение "Неизвестный вид обработки";
		КонецЕсли;
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ВидОбработки = "ПечатнаяФорма" Тогда
			ДвоичныеДанные = Выборка.ХранилищеВнешнейОбработкиДополнительное.Получить();
			НомерСтроки = Выборка.НомерСтроки;
			Если ДвоичныеДанные = Неопределено Тогда
				ДвоичныеДанные = Выборка.ХранилищеВнешнейОбработки.Получить();
				НомерСтроки = 0;
			КонецЕсли;
		Иначе
			ДвоичныеДанные = Выборка.ХранилищеВнешнейОбработки.Получить();
			НомерСтроки = Выборка.НомерСтроки;
		КонецЕсли;
		
		УИД = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		
		СтароеИмя = УИД + "_" + НомерСтроки;
		Если НЕ СтарыеИмена.Получить(СтароеИмя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДвоичныеДанные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НовоеИмя = Префикс + НовоеИмя(Выборка.Наименование);
		Если НЕ НовыеИмена.Получить(НовоеИмя) = Неопределено Тогда
			Подбор = "";
			Для сч = 1 По 5 Цикл
				Если НовыеИмена.Получить(НовоеИмя + "_" + сч) = Неопределено Тогда
					Подбор = "_" + сч;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ПустаяСтрока(Подбор) Тогда
				Сообщить("ОШИБКА ПОДБОРА ИМЕНИ: " + Выборка.Код);
				ВызватьИсключение "ОшибкаПодбораИмени";
			КонецЕсли;
			НовоеИмя = НовоеИмя + Подбор;
		КонецЕсли;
		
		НовыеИмена.Вставить(НовоеИмя, Новый Структура("УИД,НомерСтроки", УИД, НомерСтроки));
		СтарыеИмена.Вставить(СтароеИмя, НовоеИмя);
		
		ИмяФайла = НовоеИмя + "." + Расширение;
		ПолноеИмяФайла = КореньВыгрузки + "\" + ИмяФайла;
		ДвоичныеДанные.Записать(ПолноеИмяФайла);
		
		Т.ДобавитьСтроку("<" + Тэг + ">" + НовоеИмя + "</" + Тэг + ">");
			
		Если НЕ ПустаяСтрока(ПрефиксНастроек) Тогда
			
			Попытка
				Обработка = Менеджер.Создать(ПолноеИмяФайла);
				ИмяОбработки = Обработка.Метаданные().Имя;
			Исключение
				Сообщить("Не удалось обработать " + Выборка.Код);
				Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
			НаборЗаписей = РегистрыСведений.СохраненныеНастройки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИмяОбъекта.Установить(ПрефиксНастроек + ИмяОбработки);
			НаборЗаписей.Прочитать();
			НовоеИмяОбъекта = ПрефиксНовыхНастроек + НовоеИмя;
			НаборЗаписей.Отбор.ИмяОбъекта.Установить(НовоеИмяОбъекта);
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ИмяОбъекта = НовоеИмяОбъекта;
			КонецЦикла;
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
		ВнешняяОбработкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если НомерСтроки = 0 Тогда
			ВнешняяОбработкаОбъект.ИмяВнутреннейОбработки = НовоеИмя;
		Иначе
			ВнешняяОбработкаОбъект.Принадлежность[НомерСтроки - 1].ИмяВнутреннейОбработки = НовоеИмя;
		КонецЕсли;
		ВнешняяОбработкаОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция НовоеИмя(Имя)
	
	Новое = "";
	ПредПробел = Истина;
	Для сч = 1 По СтрДлина(Имя) Цикл
		Символ = Символ(КодСимвола(Имя, сч));
		Если ВРег(Символ) <> НРег(Символ) ИЛИ Найти(" 0123456789", Символ) > 0 Тогда
			Если ПредПробел Тогда
				Новое = Новое + ВРег(Символ);
				ПредПробел = Ложь;
			Иначе
				Новое = Новое + Символ;
			КонецЕсли;
			ПредПробел = Символ = " ";
		КонецЕсли;
	КонецЦикла;
	
	Новое = СтрЗаменить(Новое, " ", "");
	Возврат Новое;
	
КонецФункции
